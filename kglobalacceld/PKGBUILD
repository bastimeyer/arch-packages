# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kglobalacceld
#_revision='branch=Plasma/6.1'
_revision='commit=8643077d0422c97ad12dc052202c9e1348e99b23'
pkgver=6.1.2.r2.g8643077
pkgrel=1
pkgdesc='Daemon providing Global Keyboard Shortcut (Accelerator) functionality'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         kio
         kjobwidgets
         kservice
         kwindowsystem
         libx11
         libxcb
         qt6-base
         xcb-util-keysyms)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(
  "${pkgname}::git+https://invent.kde.org/plasma/${pkgname}.git#${_revision}"
)
sha256sums=(
  'SKIP'
)
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

pkgver() {
  cd "$pkgname"
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "$srcdir/$src"
  done

  local ver
  ver=$(echo "${pkgver}" | grep -Eo '^[0-9]+\.[0-9]+\.[0-9]')
  sed -Ei \
    -e 's/set\(PROJECT_VERSION "[^"]+"\)/set(PROJECT_VERSION "'"${ver}"'")/' \
    CMakeLists.txt
}

build() {
  cmake -B build -S "$pkgname" \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
