# Maintainer: Sebastian Meyer <mail@bastimeyer.de>
# Contributor: zan <zan@420blaze.it>
# Contributor: Jacob Henner <code@ventricle.us>
# Contributor: Eduardo Sánchez Muñoz
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

# Based on clementine-git AUR PKGBUILD

_srcname=clementine
pkgname=${_srcname}-git
pkgver=1.4.0rc1.r882.gbf4ac0cb4.0.gbf4ac0cb4
pkgrel=1
pkgdesc='A modern music player and library organizer'
url='https://www.clementine-player.org/'
license=(GPL)
arch=(x86_64)
depends=(
    alsa-lib
    chromaprint
    crypto++
    gst-plugins-base-libs
    hicolor-icon-theme
    libpulse
    protobuf
    qt5-x11extras
    udisks2-qt5
    taglib
)
makedepends=(
    boost
    cmake
    git
    qt5-tools
)
conflicts=("${_srcname}")
replaces=("${_srcname}")

source=(
    "${_srcname}::git+https://github.com/clementine-player/Clementine.git"
    'cpp17.patch'
    'volume-up-down-adjustment.patch'
    'disable-guess-album.patch'
)
sha256sums=(
    'SKIP'
    'a9f1024c178d688cdb6ef3825504aa8b55638588f993862f93713e74ee573f27'
    'ab9663becb509379c9e05990de00881ed53096e0e77824218564d66b8059d507'
    '42ffd68d0428992436ebed879f2b1833a4c471f1a78e642d36e2830e675d8bd9'
)


pkgver() {
    cd "${_srcname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_srcname}"
    sed -e 's|CRYPTOPP cryptopp|CRYPTOPP libcryptopp|' -i CMakeLists.txt # Fix crypto++ detection
    patch -p1 -i ../cpp17.patch
    patch -p1 -i ../volume-up-down-adjustment.patch
    patch -p1 -i ../disable-guess-album.patch
}

build() {
    args=(
        -DCMAKE_INSTALL_PREFIX=/usr
        -DBUILD_WERROR=off
        -DCMAKE_BUILD_TYPE=Release
        -DENABLE_GOOGLE_DRIVE=off
        -DENABLE_DROPBOX=off
        -DENABLE_SKYDRIVE=off
        -DENABLE_BOX=off
        -DENABLE_SEAFILE=off
        -DENABLE_AUDIOCD=off
        -DENABLE_LIBGPOD=off
        -DENABLE_GIO=off
        -DENABLE_LIBMTP=off
        -DENABLE_LIBLASTFM=off
        -DENABLE_WIIMOTEDEV=off
        -DENABLE_UDISKS2=on
        -DENABLE_SPOTIFY_BLOB=off
        -DENABLE_SPARKLE=off
        -DENABLE_VISUALISATIONS=off
        -DUSE_SYSTEM_TAGLIB=ON
    )
    export LDFLAGS="${LDFLAGS} -Wl,--copy-dt-needed-entries"
    cmake -B build -S "${_srcname}" "${args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
