# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Phillip Schichtel <phillip.public@schich.tel>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=adapta-gtk-theme-git
pkgver=3.93.0.117.r1.13cc593f
pkgrel=1
pkgdesc='An adaptive Gtk+ theme based on Material Design Guidelines'
arch=('any')
url='https://github.com/adapta-project/adapta-gtk-theme'
license=('GPL2')
depends=('gtk-engine-murrine' 'gtk3')
makedepends=('git' 'gnome-shell' 'inkscape' 'libxml2' 'parallel' 'sassc')
optdepends=('gnome-shell: The GNOME Shell'
            'gnome-flashback: The GNOME flashback shell'
            'budgie-desktop: The Budgie desktop'
            'cinnamon: The Cinnamon desktop'
            'xfdesktop: The Xfce desktop')
provides=('adapta-gtk-theme')
conflicts=('adapta-gtk-theme')
source=('git+https://github.com/adapta-project/adapta-gtk-theme.git')
sha256sums=('SKIP')

pkgver() {
  cd adapta-gtk-theme

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  cd adapta-gtk-theme

  patch -p1 < "${startdir}/0000-cinnamon-changes.patch"

  ./autogen.sh \
    --prefix='/usr' \
    --enable-cinnamon \
    --enable-chrome \
    --enable-gnome \
    --enable-parallel \
    --disable-flashback \
    --disable-mate \
    --disable-openbox \
    --disable-telegram \
    --disable-unity \
    --disable-xfce
  make
}

package() {
  cd adapta-gtk-theme

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
