# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=fish
pkgver=4.0.2
pkgrel=2
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
url='https://fishshell.com/'
arch=('x86_64')
license=('GPL-2.0-only AND BSD-3-Clause AND ISC AND MIT AND PSF-2.0')
depends=('glibc' 'gcc-libs' 'ncurses' 'pcre2')
optdepends=('python: man page completion parser / web config tool'
            'pkgfile: command-not-found hook'
            'groff: --help for built-in commmands'
            'mandoc: --help for built-in commmands (alternative)'
            'xsel: X11 clipboard integration'
            'xclip: X11 clipboard integration (alternative)'
            'wl-clipboard: Wayland clipboard integration')
makedepends=('cmake' 'git' 'jq' 'ninja' 'rust' 'python-sphinx')
checkdepends=('expect' 'procps-ng')
options=(!lto)
install=fish.install
backup=(etc/fish/config.fish)
source=("${pkgname}::git+https://github.com/fish-shell/fish-shell#tag=${pkgver}?signed")
validpgpkeys=(003837986104878835FA516D7A67D962D88A709A) # David Adam <zanchey@gmail.com>
sha512sums=('ff2c735649b8aaf8eddd9bcacccead02be0392483e666144fd5e35bb32664f126df9380c9e7543913cf947ef3a9f588b9cc2192e5358eb1299908555521500e5')
b2sums=('809376438c9304e1bc1284b19f5c42dd8c3f066664ed47c497e4cbfef5d45377809c8fbf3ef4fab069c2edc4597a99fe8003bf714f76e3989251a740af442907')

prepare() {
  cd ${pkgname}
  git cherry-pick --no-commit db323348c7c552bfe56e3d8ee16042401b7f1fb8
}

build() {
  cd ${pkgname}

  export CXXFLAGS+=" ${CPPFLAGS}"

  local cmake_options=(
    -G Ninja
    -B build
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_SYSCONFDIR=/etc
    -DCMAKE_BUILD_TYPE=Release
    -DBUILD_DOCS=True
    -DFISH_USE_SYSTEM_PCRE2=ON
    -DWITH_GETTEXT=ON
    -Wno-dev
  )
  cmake "${cmake_options[@]}"
  ninja -C build
}

package() {
  cd ${pkgname}

  DESTDIR="$pkgdir" cmake --install build
}

# vim: ts=2 sw=2 et:
