# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=kdecoration
pkgver=5.27.4_r335.gf7498ce
pkgrel=1
pkgdesc='Plugin based library to create window decorations'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(ki18n kcoreaddons)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("kdecoration::git+https://invent.kde.org/plasma/kdecoration.git#branch=Plasma/5.27")
sha256sums=('SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

pkgver() {
  cd kdecoration
  local _ver
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d\" -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S kdecoration \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
