# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=kwin
#_revision='branch=Plasma/6.5'
_revision='commit=7a15f3d6c97fbc7dde29d49c06ee710879bb9d09'
pkgver=6.5.3.r16.g7a15f3d
pkgrel=1
pkgdesc='An easy to use, but flexible, Wayland compositor'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(aurorae
         breeze
         gcc-libs
         glibc
         plasma-activities
         kauth
         kcmutils
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kdeclarative
         kdecoration
         kglobalaccel
         kglobalacceld
         kguiaddons
         ki18n
         kidletime
         kirigami
         kitemmodels
         knewstuff
         knighttime
         knotifications
         kpackage
         kquickcharts
         kscreenlocker
         kservice
         ksvg
         kwayland
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         lcms2
         libcanberra
         libdisplay-info
         libdrm
         libei
         libepoxy
         libinput
         libpipewire
         libqaccessibilityclient-qt6
         libxcb
         libxcvt
         libxkbcommon
         mesa
         pipewire-session-manager
         libplasma
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-sensors
         qt6-svg
         qt6-tools
         qt6-wayland
         systemd-libs
         wayland
         xcb-util-keysyms
         xcb-util-wm)
makedepends=(ninja
             extra-cmake-modules
             kdoctools
             krunner
             plasma-wayland-protocols
             python
             wayland-protocols
             xorg-xwayland)
optdepends=('maliit-keyboard: virtual keyboard')
groups=(plasma)
source=(
  "${pkgname}::git+https://invent.kde.org/plasma/${pkgname}.git#${_revision}"
  dont-build-docs.patch
  effects-slide-springconstants.patch
  effects-overview-no-gaps-no-shadow-no-border.patch
  effects-overview-no-add-remove-hotkeys.patch
  effects-windowheapdelegate-no-close-button.patch
  effects-windowheapdelegate-no-caption.patch
  virtualdesktops-increase-maximum.patch
  "compositor-make-cursor-repaints-async.patch::https://bugsfiles.kde.org/attachment.cgi?id=186350"
)
install=$pkgname.install
sha256sums=(
  SKIP
  5d9fd68d91cab46b81dca8dade8a59834fa9c25f2cb709ea5f1efabbef4b2579
  3c8a202a172ff08a8dc21da5456c1aab4bf44e1e4645636ddf53fb092ff3211b
  91928d9cc5fbb920b2df518801352d801c729bfc9c466aec90f0d512e88c91e4
  4f38e7e68a0d031b04f54599440f8086b4c76a04ee93afe1c1eccc442ffc92d0
  0f452a461bf434ebfe3ec0142682a4f159234767b7a67f9d7e55ef921148a84a
  590ee4f95044ad8ab3005e2470479af0f18c743cf76bf433ed3e4743e4aa7533
  64b0379c5f011cd2339dcd62a63b4cce1c2131e4a36828497ff42b58e944650e
  8fe63ecaef8174749fd830de972cc012f6b028eb91358f4f7ee47ffa68a8caaa
)
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1'  # Nicolas Fella <nicolas.fella@kde>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

pkgver() {
  cd "$pkgname"
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "$srcdir/$src"
  done

  local ver
  ver=$(echo "${pkgver}" | grep -Eo '^[0-9]+\.[0-9]+\.[0-9]')
  sed -Ei \
    -e 's/set\(PROJECT_VERSION "[^"]+"\)/set(PROJECT_VERSION "'"${ver}"'")/' \
    CMakeLists.txt
}

build() {
  cmake -B build -S "$pkgname" -G Ninja \
    -DCMAKE_AUTOGEN_PARALLEL=1 \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
