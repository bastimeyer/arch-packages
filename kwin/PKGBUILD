# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=kwin
pkgver=5.23.1
pkgrel=1
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils kwayland-server breeze
         pipewire-media-session libqaccessibilityclient lcms2)
makedepends=(extra-cmake-modules qt5-tools kdoctools krunner)
optdepends=('maliit-keyboard: virtual keyboard for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        # revert desktop grid fixes
        # this fixes the present windows effect animation in the desktop grid
        'revert-ba163e40-72ebe976-0042d2d8.patch'
        # fix desktop grid effect zoom-in and zoom-out animation on reverted fixes
        'desktopgrid-overlaying-tile-fix.patch')
install=$pkgname.install
sha256sums=('5838d7ab84babc5bd3cb3df9f7e38fc01c98ba1bf584b6d43e4528dd03598b31'
            'SKIP'
            '45ccbe070565e4bd4785e80871cca4474aa9b72fa09968405429e282bac95e15'
            'ed392f7d87b5a1ebe7c0bd8625d5f3d5c47ebd3215b0156a4185c76d5143f300')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'B3CB366552540BE06EE9AD9711968C44928CAEFC'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>


prepare() {
  cd $pkgname-$pkgver
  patch -p1 < "${srcdir}/revert-ba163e40-72ebe976-0042d2d8.patch"
  patch -p1 < "${srcdir}/desktopgrid-overlaying-tile-fix.patch"
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
