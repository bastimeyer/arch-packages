# Contributor: Sebastian Meyer <mail@bastimeyer.de>
# Contributor: Dongsheng Cai <imdongsheng@gmail.com>
# Maintainer: Adria Arrufat (swiftscythe) <swiftscythe@gmail.com>

_pkgname=gthumb
pkgname="${_pkgname}-git"
pkgver=3.6.1.64.g4dff961a
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
url="http://live.gnome.org/gthumb/"
license=('GPL')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
depends=(
    clutter-gtk
    dconf
    desktop-file-utils
    gsettings-desktop-schemas
    gst-plugins-base-libs
    hicolor-icon-theme
    librsvg
    libwebp
)
makedepends=(
    docbook-xsl
    exiv2
    git
    gnome-common
    intltool
    itstool
    libraw
    librsvg
    meson
    yelp-tools
)
optdepends=(
    'exiv2: metadata support'
    'libraw: read RAW files'
)
options=('!emptydirs')
install=gthumb.install
source=($_pkgname::git+https://git.gnome.org/browse/$_pkgname)
md5sums=('SKIP')

pkgver() {
    cd $_pkgname/
    git describe | sed 's/^v//;s/-/./g'
}

prepare() {
    cd $_pkgname/
    patch -p1 < "${startdir}/0000-disable-web-albums.patch"
    patch -p1 < "${startdir}/0000-zoom-fixes.patch"
    mkdir -p build
    cd build/
    meson setup .. \
        -Dprefix=/usr \
        -Dlibsecret=false \
        -Dwebservices=false \
        -Dlibbrasero=false
}

build() {
    cd $_pkgname/build/
    ninja
}

package() {
    cd $_pkgname/build/
    DESTDIR="$pkgdir" ninja install
}
