# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-declarative
_qtver=5.14.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
depends=('qt5-base')
makedepends=('python' 'vulkan-headers')
groups=('qt' 'qt5')
conflicts=('qtchooser')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=(
  "https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
  40ba29c.patch
)
sha256sums=(
  'a3c4617adc9760347c93d2eb6c25d22f620cd22f44afa0494eb499a805831650'
  '59fc765dcf74a47b58209c77efbc2c41f352b637bd9db92a8312e816ddb8c72f'
)

prepare() {
  mkdir -p build
  cd ${_pkgfqn}
  patch -Np1 < ../40ba29c.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Symlinks for backwards compatibility
  for b in "$pkgdir"/usr/bin/*; do
    ln -s $(basename $b) "$pkgdir"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
