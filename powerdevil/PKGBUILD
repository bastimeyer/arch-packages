# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

pkgname=powerdevil
#_revision='branch=Plasma/6.0'
_revision='commit=497d60fa066e79f5067485ff376e16f4194a6faf'
pkgver=6.0.2_r3163.g497d60fa
pkgrel=1
pkgdesc='Manages the power consumption settings of a Plasma Shell'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(ddcutil
         gcc-libs
         glibc
         plasma-activities
         kauth
         kcmutils
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         ki18n
         kidletime
         kirigami
         kitemmodels
         knotifications
         ksvg
         kwindowsystem
         kxmlgui
         layer-shell-qt
         libkscreen
         libplasma
         libxcb
         plasma-workspace
         qt6-base
         qt6-declarative
         solid
         systemd-libs)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('kinfocenter: for the Energy Information KCM'
            'power-profiles-daemon: power profiles support')
groups=(plasma)
source=(
  "$pkgname::git+https://invent.kde.org/plasma/$pkgname.git#${_revision}"
  '0000-finer-brightness-control.patch'
)
sha256sums=(
  'SKIP'
  '16cc891e4b885325341843f68398721f405f63521b88967856a86979d2c0869c'
)
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

pkgver() {
  cd "$pkgname"
  local _ver
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d\" -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "$srcdir/$src"
  done
}

build() {
  cmake -B build -S "$pkgname" \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
