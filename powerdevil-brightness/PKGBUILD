# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sebastian Meyer <mail@bastimeyer.de>

_pkgname=powerdevil
pkgname=powerdevil-brightness
pkgver=5.21.3
pkgrel=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
pkgdesc='Manages the power consumption settings of a Plasma Shell'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(plasma-workspace bluez-qt networkmanager-qt ddcutil)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kinfocenter: for the Energy Information KCM')
groups=(plasma)
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"{,.sig}
  0000-finer-brightness-control.patch
)
sha256sums=('ffcbc2ca715377e6652f65744601c95ba0babbde7a15d4f4e8a3fa0ad85e6652'
            'SKIP'
            '16cc891e4b885325341843f68398721f405f63521b88967856a86979d2c0869c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  cd "${_pkgname}-${pkgver}"
  patch -p1 < "${srcdir}/0000-finer-brightness-control.patch"
}

build() {
  cmake -B build -S "${_pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DHAVE_DDCUTIL=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
